; EPIC demonstration program - rev 1.1
;  Copyright (C)1995 Jeff Schmoyer - 12/30/94
;   last update 2/13/96 - written in PIC Macro Assembler
;
; this program for the pic16c84 will send a continuous
; binary count to port b at about a one second rate.
; if leds are connected to each pin of port b in series
; with a 470 ohm resistor to +5 volts, they will blink
; in binary sequence.
;
	maclib  'p84.inc'
;
	device  pic16c84,rc_osc,wdt_off,pwrt_on,protect_off
;
leds    =       portb           ;led output port
;
dir_a   =       01fh            ;all in
dir_b   =       00h             ;all out
;
	org     0ch             ;start of ram space
;
binary  ds      1               ;binary count
cnt_low ds      1               ;delay counters
cnt_mid ds      1
cnt_high ds     1
;
	org     0               ;reset vector
;
	jmp     start           ;go to beginning of program on reset
;
	org     5               ;start of code space
;
	retw    'DEMO84 Rev 1.1 Copyright (C)1995 Jeffrey M. Schmoyer'
;
;        
; delay for about 1 second based on 4mhz clock cycle
;
delay   mov     cnt_high,#4
	clr     cnt_mid
	clr     cnt_low
delay1  nop
	djnz    cnt_low,delay1
	djnz    cnt_mid,delay1
	djnz    cnt_high,delay1
	ret
;
;
; it all starts here
;
start   mov     !ra,#dir_a      ;set up starting port values and direction
	mov     rb,#0ffh        ;start with all leds off (high)
	mov     !rb,#dir_b
;
;
; main loop to send binary sequence to port b
;
	mov     binary,#0feh    ;start with first led on (on = zero)
;
mainloop mov    leds,binary     ;send the binary count to the leds
;
	call    delay           ;wait around a second
;
	dec     binary          ;decrement to next binary value
;
	jmp     mainloop        ;do it forever
;
